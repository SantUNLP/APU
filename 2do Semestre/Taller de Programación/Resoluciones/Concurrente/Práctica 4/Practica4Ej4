{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  proceso juntarPapeles
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  proceso depositarPapeles
  comenzar
    mientras HayPapelEnLaBolsa
      depositarPapel   
  fin
  proceso depositarFlores
  comenzar
    mientras HayFlorEnLaBolsa
      depositarFlor
  fin
  proceso vaciarBolsa
  comenzar
    depositarPapeles
    depositarFlores
  fin
areas
  AreaJ: AreaP(1,1,1,1)
  AreaT1: AreaP(2,1,2,1)
  AreaT2: AreaP(3,1,3,1)
  Cuadrante: AreaPC(2,2,100,100)
robots
  robot trabajador
  variables
    ID,tarea,av,ca,ai,ci:numero
    finalizar:boolean
  comenzar
    finalizar:=F
    RecibirMensaje(ID,j)
    ai:=PosAv
    ci:=PosCa
    mientras (finalizar = F)
      RecibirMensaje(tarea,j)
      si tarea=4
        finalizar:=V
      sino
        RecibirMensaje(av,j)
        RecibirMensaje(ca,j)
        si tarea=1
          BloquearEsquina(av,ca)
          Pos(av,ca)
          juntarFlores
          Pos(ai,ci)
          LiberarEsquina(av,ca)
        sino
          si tarea=2
            BloquearEsquina(av,ca)
            Pos(av,ca)
            juntarPapeles
            Pos(ai,ci)
            LiberarEsquina(av,ca)
          sino
            BloquearEsquina(av,ca)
            Pos(av,ca)
            vaciarBolsa
            Pos(ai,ci)
            LiberarEsquina(av,ca)
  fin
  robot jefe
  variables
    ID,tarea,av,ca:numero
  comenzar
    ID:=1
    EnviarMensaje(ID,t1)
    ID:=2
    EnviarMensaje(ID,t2)
    repetir 10
      Random(tarea,1,3)
      Random(ID,1,2)
      Random(av,2,100)
      Random(ca,2,100)
      si ID=1
        EnviarMensaje(tarea,t1)
        EnviarMensaje(av,t1)
        EnviarMensaje(ca,t1)
      sino
        EnviarMensaje(tarea,t2)
        EnviarMensaje(av,t2)
        EnviarMensaje(ca,t2)
    EnviarMensaje(4,t1)
    EnviarMensaje(4,t2)
  fin
variables
  t1,t2:trabajador
  j:jefe
comenzar
  AsignarArea(j,AreaJ)
  AsignarArea(t1,AreaT1)
  AsignarArea(t2,AreaT2)
  AsignarArea(t1,Cuadrante)
  AsignarArea(t2,Cuadrante)
  Iniciar(t1,2,1)
  Iniciar(t2,3,1)
  Iniciar(j,1,1)
fin