{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores(ES flores:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores+1
  fin
  proceso recojerFlores(ES flores:numero)
  comenzar
    BloquearEsquina(50,50)
    Pos(50,50)
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores+1
    Pos(100,100)
    LiberarEsquina(50,50)
  fin
areas
  areaJefe: AreaP (100,100,100,100)
  deposito: AreaPC (50,50,50,50)
  area1: AreaP (1,1,7,100)
  area2: AreaP (10,1,16,100)
  area3: AreaP (20,1,26,100)
robots
  robot floreros
  variables
    ID,tam,flores,av,ca:numero
  comenzar
    flores:=0
    RecibirMensaje(ID,jefe)
    repetir 3
      RecibirMensaje(tam,jefe)
      repetir 4
        repetir tam
          juntarFlores(flores)
          mover
        derecha
        juntarFlores(flores)
      mientras HayFlorEnLaBolsa
        av:=PosAv
        ca:=PosCa
        BloquearEsquina(50,50)
        Pos(50,50)
        depositarFlor
        Pos(av,ca)
        LiberarEsquina(50,50)
      EnviarMensaje(ID,jefe)
      Pos(PosAv,PosCa+tam+1)
  fin
  robot coordinador
  variables
    ID,f1,f2,f3,flores:numero
    tam:numero
  comenzar
    ID:=1
    EnviarMensaje(ID,r1)
    ID:=2
    EnviarMensaje(ID,r2)
    ID:=3
    EnviarMensaje(ID,r3)
    repetir 3
      Random(tam,1,6)
      EnviarMensaje(tam,r1)
      Random(tam,1,6)
      EnviarMensaje(tam,r2)
      Random(tam,1,6)
      EnviarMensaje(tam,r3)
      repetir 3
        flores:=0
        RecibirMensaje(ID,*)
        si ID=1
          recojerFlores(flores)
          f1:=f1+flores
        sino
          si ID=2
            recojerFlores(flores)
            f2:=f2+flores
          sino
            recojerFlores(flores)
            f3:=f3+flores
    si (f1>f2) & (f1>f3)
      Informar("Gano ROBOT1",f1)
    sino
      si (f2>f1) & (f2>f3)
        Informar("Gano ROBOT2",f2)
      sino
        Informar("Gano ROBOT3",f3)
  fin
variables
  r1,r2,r3:floreros
  jefe:coordinador
comenzar
  AsignarArea(jefe,areaJefe)
  AsignarArea(jefe,deposito)
  AsignarArea(r1,area1)
  AsignarArea(r1,deposito)
  AsignarArea(r2,area2)
  AsignarArea(r2,deposito)
  AsignarArea(r3,area3)
  AsignarArea(r3,deposito)
  Iniciar(jefe,100,100)
  Iniciar(r1,1,1)
  Iniciar(r2,10,1)
  Iniciar(r3,20,1)
fin